plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
    id 'org.jetbrains.dokka' version '1.5.0'
}

android {
    namespace 'com.maxwilk.maven_test_library'
    compileSdk 34
    version '1.0.0'

    defaultConfig {
        minSdk 24
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildToolsVersion '34.0.0'
}

apply plugin: 'org.jetbrains.dokka'
task dokkaJavadocJar(type: Jar, dependsOn: 'dokkaJavadoc') {
    archiveClassifier.set('javadoc')
    from tasks.dokkaJavadoc.outputDirectory
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.maxwilk'
            artifactId = 'maven-test-library'

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            artifact sourcesJar
            artifact dokkaJavadocJar

            pom {
                name = 'maven-test-library'
                description = 'Test library'
                url = 'https://github.com/MaxWilk/maven-test-library'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'maxwilk'
                        name = 'Max Wilk'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/MaxWilk/maven-test-library.git'
                    developerConnection = 'scm:git:ssh://github.com/MaxWilk/maven-test-library.git'
                    url = 'https://github.com/MaxWilk/maven-test-library'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        verify = true
        files = true
        artifacts = true
        checksums = true
    }
    release {
        github {
            token = 'test'
        }
    }
    deploy {
        maven {
            mavenCentral {
                app {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
